import org.jetbrains.changelog.Changelog

plugins {
	id 'fabric-loom' version '1.7-SNAPSHOT'
	id "me.modmuss50.mod-publish-plugin" version "0.7.4"
	id 'org.jetbrains.changelog' version '2.+'
}

archivesBaseName = project.archives_base_name
version = project.mod_version + "+" + project.minecraft_version
group = project.maven_group

repositories {
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"instantfeedback" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}

}

fabricApi {
	configureDataGeneration()
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}


publishMods {
	file = remapJar.archiveFile
	type = STABLE
	changelog = fetchChangelog()

	displayName = "Instant Feedback ${version.get()}"
	modLoaders.add("fabric")
	modLoaders.add("quilt")

	curseforge {
		accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
		projectId = "1116683"
		minecraftVersions.add(project.curseforge_minecraft_version)
		requires("fabric-api")
	}
	modrinth {
		accessToken = providers.environmentVariable("MODRINTH_TOKEN")
		projectId = "pcDsJNzK"
		minecraftVersions.add(project.minecraft_version)
		requires("fabric-api")
	}
	github {
		accessToken = providers.environmentVariable("GITHUB_TOKEN")
		repository = providers.environmentVariable("GITHUB_REPOSITORY").getOrElse("DrexHD/dryrun")
		commitish = providers.environmentVariable("GITHUB_REF_NAME").getOrElse("dryrun")
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

private String fetchChangelog() {
	def log = getChangelog.changelog.get()
	if (log.has(project.mod_version)) {
		return log.renderItem(
				log.get(project.mod_version).withHeader(false),
				Changelog.OutputType.MARKDOWN
		)
	} else {
		return ""
	}
}